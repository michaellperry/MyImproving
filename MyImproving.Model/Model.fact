namespace MyImproving.Model;

fact Individual {
key:
    string anonymousId;

query:
	Company* companies {
		Director d : d.individual = this
		Company c : c = d.company
	}

	Game* games {
		Director d : d.individual = this
		Company c : c = d.company
		Game g : g.companies = c
	}
}

fact Domain {
key:
	string name;

query:
	Company* companies {
		Company c : c.domain = this
	}

	Game* games {
		Game g : g.domain = this
	}
}

fact Company {
key:
	unique;
	publish Domain domain;

mutable:
	string name;

query:
	Game* games {
		Game g : g.companies = this
	}
}

fact Director {
key:
	publish Individual individual;
	Company company;
}

fact Game {
key:
	unique;
	publish Domain domain;
	publish Company* companies;

mutable:
	string name;

query:
	Round* rounds {
		Round r : r.game = this
	}
}

fact Round {
key:
	publish Game game;
	int index;

query:
	Candidate* candidates {
		Candidate c : c.round = this
	}

	Offer* offers {
		Offer o : o.candidate.round = this
	}
}

fact Turn {
key:
	publish Company company;
	Round round;
}

fact Borrow {
key:
	Turn turn;
	int amount;
}

fact Repay {
key:
	Turn turn;
	int amount;
}

fact Candidate {
key:
	unique;
	Round round;
	int skill;
	int relationship;
}

fact Offer {
key:
	Turn turn;
	Candidate candidate;
	int chances;
}

fact Hire {
key:
	Offer offer;
}

fact Quit {
key:
	Hire hire;
	Turn turn;
}

fact Payroll {
key:
	Turn turn;
	Hire hire;
}

fact Resource {
key:
	int skill;
	int need;
	int revenue;
}

fact Gig {
key:
	unique;
	Round round;
	int duration;
	int term;
	Resource* resources;
}

fact Bid {
key:
	Turn turn;
	Gig gig;
	int chances;
}

fact Win {
key:
	Bid bid;
}

fact Lose {
key:
	Win win;
	Turn turn;
}

fact Revenue {
key:
	Win win;
	Turn turn;
}
